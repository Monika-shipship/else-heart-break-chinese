# SPRAK 编程语言手册

## 介绍

编程，多么美妙的事情！啊... 单单通过代码的魔法咒语就能控制一切的乐趣。

本手册将教你所有你需要知道的，让你成为比特的巫师，逻辑的大师，以及数据和函数的统治者。

共有10个章节，我们建议你按顺序阅读，因为后面的章节内容会建立在前面章节的基础上。以下是我们将要学习的内容概览：

1.  解决问题的介绍
2.  变量
3.  数学运算
4.  函数
5.  If语句
6.  数组
7.  循环
8.  处理文本
9.  对象和方法

你将要学习的语言叫做 SPRAK。我们希望你会觉得它用起来既有趣又有启发！

## 第 1 章 --- 解决问题的介绍

你如何解决一个问题？这确实是个棘手的问题。毕竟，每个问题都是独特的，需要它自己独特的解决方案。或者真的是这样吗？

在编程中，许多相似、相关的问题会一次又一次地出现。一个有经验的程序员可能根本不需要有意识地去思考如何解决这些情况。正确的答案就出现在他或她的脑海里，并且可以毫不费力地输入到电脑里。

但是，一个人如何学会这样的壮举呢？“读代码！”是年轻和缺乏经验的程序员经常重复的口头禅。这个建议当然非常好，但在实践中可能很难遵循。许多现实世界的解决方案既混乱又结构不良，有许多奇怪的细节使它们难以阅读和理解。

没有指导，一页代码对于除了作者之外的任何人几乎是不可能理解的（有时甚至包括作者本人）。尽管如此，尝试理解别人的代码并从中学习零碎的知识，试图找到模式并探索另一个人的解决方案，这仍然是非常值得的。

一个非常好的技巧确实是重写某人的代码，用你自己的风格和改进。这极大地有助于理解书面代码中的流程和联系。

但回到解决问题！解决问题的主要方法，无论是在编程中还是在其他方面，都是解构问题并单独解决每个组件。这个简单的方法已经被证明非常有效，并且是函数式组合（见第5章）和面向对象（见第9章）的核心。

通过将事物分解成越来越小的部分，一个庞大而纠结的东西可以变得相当小而易于管理。如何做好这一点是我们在这篇文章中将反复提及的。通过分析手头的问题，然后尝试将其拆解并用你将在后续章节中学到的不同结构将其重新组合，希望你也能成为解决问题的大师！

最后，如果什么都不起作用，一个解决方案完全让你束手无策——打个盹，离开你的机器，去散步或者吃个三明治。没有什么比睡眠和新鲜空气更能帮助解决问题了。

## 第 2 章 --- 变量

变量用于给事物命名。这可能看起来像一个微不足道的任务，但不要被欺骗，命名事物的能力是编程中最重要的事情之一。原因是它允许我们以符号化的方式抽象和处理我们解决方案的组件。

例如：与其将你的名字称为像“Lisa”这样的字符串，我们可以创建一个包含这个值的变量。这个变量可能被称为'name'之类的东西，并且可以在不同的时间点引用不同的名字，这取决于程序的需求。

以下是一些如何创建变量的例子：

```sprak
number x = 10
string myName = "Simon"
bool itIsChristmas = false
```

注意左边的词，比如'number'，'string'和'bool'。那些是类型，它们描述了我们想要放入变量中的数据类型。之后是变量名。这应该是一个描述性的东西，解释了我们放入变量中的是什么样的东西。

接下来是等号，'='。这意味着我们将变量的值设置为右边的任何东西。

变量可以通过使用等号重新设置为其他值，像这样：

```sprak
x = 11
myName = "Simone"
itIsChristmas = true
```

这要求变量已经在程序的前面部分使用上面的语法声明过，带有类型和名称（例如'number x = 10'）。

如果一个变量包含一个数字类型，它可以为了方便而以一些额外的方式改变，节省几次按键：

```sprak
x++      # 增加一
x += 10  # 增加10
x--      # 减少一
x -= 20  # 减少20
```

## 第 3 章 --- 数学运算

别让这一章吓到你！你不需要是个数学奇才才能编程。好奇心和逻辑思维会带你走得更远，相信我。

尽管如此，在你的代码中执行不同种类的数学运算通常是非常有用的。

基本的运算有：

```
+  # 加
-  # 减
*  # 乘
/  # 除
```

这些运算可以以下列方式使用：

```sprak
number x = 10 + 20
number y = x * 2
number z = y - 1
```

现在 x 的值是 30，y 是 60，z 是 59。确保你理解了为什么会这样，以及每个变量是如何依赖于在它之前定义的变量的。

你也可以构建更复杂的复合数学语句，如下所示：

```sprak
x = 10 / y * z + 1
```

运算符遵循常规的运算符优先级，这意味着 * 和 / 会在 + 和 - 之前计算。

要改变这一点，可以使用括号：

```sprak
x = 10 / y * (z + 1)
```

不同的计算机系统可能有其他可用的数学运算作为函数（见第5章）。

还有一些非常有用的数学运算符，如下所示：

```
<   # 小于
<=  # 小于或等于
>   # 大于
>=  # 大于或等于
==  # 等于
!=  # 不等于
```

它们的用法类似于 +、-、* 和 /，但是它们返回 true 或 false 作为结果：

```sprak
30 < 20  # false (假)
5 <= 5   # true (真)
100 != 0 # true (真)
```

## 第 4 章 --- 函数

函数是编程中抽象和简化的主要工具。基本上，它通过将某些东西变成一个自包含的单元并对其进行参数化，从而赋予你的代码超能力。这一切听起来很抽象，也许本身并没有什么用，所以让我们看一些例子。

首先，这是你如何调用一个函数：

```sprak
Print("Hello!")
```

这将使计算机在屏幕上打印文本“Hello!”（不带引号）——如果它是一台带显示器的电脑的话！

在那种情况下，函数的名称是Print，参数是“Hello!”。通过使用不同的参数，我们可以让函数在每次调用时表现不同。

这个函数可能看起来有点奇怪，至少如果你习惯了数学中的函数。奇怪的是它不返回任何东西！在SPRAK中，函数不需要这样做，所以这完全没问题。

不过，我们经常想要使用返回值的函数，所以方法如下：

```sprak
number x = Max(13, 10)
```

这将把函数Max的返回值赋给变量'x'。Max接受两个参数并返回两者中较大的一个。在这种情况下，它是13，所以'x'将得到那个值。

我们也可以使用变量或其他函数作为Max函数的参数，像这样：

```sprak
number x = Max(Random(), 0.5)
```

Random函数不接受任何参数，并返回一个0.0到1.0之间的随机值。这个随机值与0.5进行比较，并选择最大的一个赋给'x'。

编写自己的函数非常简单，看起来有点像定义变量。你甚至可以说一个函数是一个包含一段代码的变量，所以这样想是合理的。

这是我尝试编写自己的函数来计算圆的面积：

```sprak
number AreaOfCircle(number radius)
    return radius * radius * 3.1415
end
```

这段代码有很多不同的部分，所以让我们把它全部分开来看。第一行解释了函数将返回什么类型的值（一个数字），函数本身的名称（'AreaOfCircle'），最后是它接受什么参数（一个名为'radius'的数字）。

用于参数的名称只在函数内部知道——使用AreaOfCircle函数的人不需要知道任何关于它的事情。

在函数内部，在第一行和最后一行包含最终'end'的行之间，我们有当这个函数被调用时将运行的代码。在这个特定的情况下，我们将'radius'与自身和Pi相乘，然后将该特定结果返回给调用者。

要使用这个函数，我们可以这样做：

```sprak
number a = AreaOfCircle(3.0)
```

变量'a'现在将包含3.0 * 3.0 * 3.1415的值，大约是28.27。

如果你想查看一个对象是否具有某个函数，你可以使用HasFunction()函数，像这样：

```sprak
if HasFunction("Print")
    Print("Hello")
end
```

## 第 5 章 --- If 语句

是时候找点乐子了！if语句是计算机能够“思考”的核心部分。它根据某件事是否为真，来选择代码的不同路径。有点像一个铁路道岔，有个小家伙检查当前的火车应该走哪条路。

例如，假设我们有一个名为'SECRET'的变量，它包含'true'或'false'的值，我们不知道是哪个。

我们怎么能找出来呢？！当然是使用if语句：

```sprak
if SECRET
    Print("true!")
end
```

这里有几个有趣的地方需要注意。首先是'if'和'end'这两个词的使用。这两个词像一对一样一起出现。'end'这个词显示了代码块的结束位置。如果名为'SECRET'的变量包含值'true'，那么介于两者之间的所有事情都会发生。在这种情况下，它会打印“true!”。另一方面，如果变量包含'false'，则什么都不会发生。

如果我们也想在这种情况下打印些什么呢？这里有一种方法：

```sprak
if SECRET
    Print("true!")
else
    Print("false!")
end
```

你可以在'if'和'end'之间的代码块中做几件事。不要被文本向右移动了一点而感到困惑，那只是为了可读性。

```sprak
if SECRET
    Print("The")
    Print("SECRET")
    Print("is")
    Print("true!")
end
```

通过使用以下表示法，可以有更多的情况：

```sprak
if a
    # 做某事
else if b
    # 做别的事情
else if c
    # 如果c为真（但a和b为假），则执行此操作
else
    # 如果a、b和c都为假，则执行此操作
end
```

最后，可以使用'and'和'or'这两个词将包含逻辑值（true / false）的多个变量或表达式一起检查：

```sprak
if a and (b or c)
    # 如果a和b为真，或者a和c为真，
    # 这将会发生
end
```

如果这看起来令人困惑，别担心，稍后再回来重读任何第一次没有理解的部分。

## 第 6 章 --- 数组

数组是一种将多个值组合成一个可以作为一个单元来操作和传递的结构的方法。它非常方便！

这是创建数组的一种方法：

```sprak
array numbers = [10, 5, 7, 123]
```

'[' 和 ']' 包围着数字或任何你想放入数组的东西，有点像一个盒子的边或其他东西...

这个数组（名为'numbers'）现在包含四个数字10、5、7和123。我们可以使用索引操作符从变量中取出它们，看起来像这样：

```sprak
Print(numbers[0])
Print(numbers[1])
Print(numbers[2])
Print(numbers[3])
```

这将打印数组中每个“槽”的内容，从第一个槽中的'10'开始，到最后一个槽中的'123'结束。请注意，数组的索引从0开始，到3结束。这是一个奇怪的标准，大多数编程语言都遵循这个标准，你只需要接受它感觉有点奇怪。

索引操作符也可以用来给数组中的某个位置赋新值：

```sprak
numbers[1] = 666
```

现在'numbers'变量将包含[10, 666, 7, 123]。小心使用，以免产生bug！以这种方式操作数组可能很棘手。

有一个名为'Count'的函数，它可以计算数组中有多少个东西。只需像这样将数组传递给'Count'：

```sprak
Count(numbers)   # 在这种情况下，函数返回4
```

还有更多使用数组的技巧和函数，但这是一个好的开始。

## 第 7 章 --- 循环

循环用于使代码块一遍又一遍地运行，像这样：

```sprak
loop
    Print("Hello!")
end
```

这将永远不停地打印“Hello!”，或者至少直到有人关掉电脑，或者类似的事情发生……

循环也可以从内部使用'break'关键字中断：

```sprak
loop
    if IsDone()
        break
    end
end
Print("Done!")
```

这段代码将一直循环，直到'IsDone'函数返回true，然后执行将继续到循环下方，程序将打印“Done!”。

循环也可以“遍历”一个数组。这非常简单，只需在'loop'关键字后添加数组：

```sprak
loop [1, 2, 3, 4, 5]
    # 做某事五次
end
```

通常你想要访问数组中的元素。那么你必须给它一个名字：

```sprak
array a = [1, 2, 3, 4, 5]

loop x in a
    Print(x)
end
```

这将打印数组中存储的每一项！如果你想循环一个数字范围，比如说从0到100，你可以使用'from'...'to'关键字来做到这一点：

```sprak
loop x from 0 to 100
    Print(x)
end
```

这比创建一个包含所有这些数字的数组要高效得多。

## 第 8 章 --- 操作文本

SPRAK 操作文本的工具很少，而且有点简单。不过，了解它们仍然很重要。没有好的文本输出和输入，很多程序会因可用性非常差而受到影响。

最重要的操作是将不同种类的数据表示为文本。这通常在函数需要文本时自动完成。例如，打印函数：

```sprak
Print(10)
```

这将输出文本“10”，正如预期的那样。更有趣的是'+'运算符，它可以用来组合更长的字符串：

```sprak
Print("X has the value " + X)
```

这将自动将X转换为字符串，并打印出类似“X has the value 42”的内容，如果X的值是42。

你也可以打印数组，但如果它们很长，它们可能会被中途截断，以适应屏幕，而不是创建巨大的字符串。

## 第 9 章 --- 对象和方法

这是本手册的最后一章，也许是最激动人心的一章。我们现在已经达到了Sprak最强大的构造，点运算符。

点运算符用于调用其他对象中的函数。这些函数通常被称为方法，以区别于普通函数。

由于这样的方法可以对其所附加的对象做一些事情，甚至可以调用其他对象中的其他方法，所以点运算符带来的力量是巨大的。

请小心使用它，尽量不要弄得一团糟。

这是你如何使用它。首先连接到另一个对象：

```sprak
var o = Connect("HomeComputer")
```

然后使用连接（在这种情况下存储在变量'o'中）与点运算符一起调用一个方法：

```sprak
o.Print("Hello at home!")
```

这将使你连接的计算机在其显示器上打印消息“Hello at home!”。

当然，这需要接入互联网。
